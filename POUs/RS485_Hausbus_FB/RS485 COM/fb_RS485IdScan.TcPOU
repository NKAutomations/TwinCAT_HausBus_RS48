<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="fb_RS485IdScan" Id="{626e2986-02cd-4da6-89a8-c21a082cde8a}" SpecialFunc="None">
    <Declaration><![CDATA[(*======================================================================================================================================*)
(*fb_RS485Search:	Suchfunktion um Automatisch alle ID's im Bus zu Scannen*)
(*
Date          	| Version    	| created under        	| Author          	| Remark *)
(*====================================================================================================================================================*)
(*03/06/2025   	| 1.00      		| TC3		  		| N.Kersting       | first release *)
(*====================================================================================================================================================*)
FUNCTION_BLOCK fb_RS485IdScan
VAR_INPUT
	I_bStartSearch		:BOOL;
	I_diStartID			:DINT	:= 5000;
	I_diMaxID			:DINT	:= 6000;
	I_sStartZeichen		:BYTE := 16#FD;										(* Startzeichen Präfix *)
	I_sEndZeichen		:BYTE := 16#FE;										(* Endzeichen Suffix *)
	I_tWait				:TIME := T#60MS;
	I_StringReceived	:STRING(255);
END_VAR
VAR_OUTPUT
	Q_bSend				:BOOL;
	Q_sSearchString		:STRING(255);
	Q_iCnt				:INT;
	Q_sState			:STRING;
	Q_sIDList			:ARRAY[1..cMaxScanIDs] OF DINT;
END_VAR
VAR
	i					:INT;
	iArrIdx				:INT := 1;
	fbPingEvent			:fb_BusPingEvent;
	diActId				:DINT;
	iStep				:INT;
	tonWait				:ton;
	rTrigSearch			:r_trig;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
tonWait(
	PT := I_tWait);
	
rTrigSearch(CLK:= I_bStartSearch, Q=> );

IF NOT I_bStartSearch
THEN
	iStep := 0;
END_IF
	
CASE iStep OF
(*=========================================================================*)
0:	Q_sState := 'INIT';
	tonWait.IN := FALSE;
	Q_bSend := FALSE;
	diActId := I_diStartID;
	Q_sSearchString := '';
	iArrIdx := 1;
	Q_iCnt := 0;
	
	IF rTrigSearch.Q
		AND I_diStartID >0
		AND I_diMaxID >0
	THEN
		FOR i := 1 TO cMaxScanIDs BY 1 DO 
			Q_sIDList[i] := 0;
		END_FOR
		
		iStep := iStep +1;
	END_IF
	
(*=========================================================================*)
1:	Q_bSend := FALSE;
	tonWait.IN := FALSE;
	
	IF diActId <= I_diMaxID
	THEN		
		iStep := iStep +1;
	ELSE
		iStep := 0;
	END_IF
	
(*=========================================================================*)
2:	Q_sSearchString := CONCAT(F_ToCHR(I_sStartZeichen),DINT_TO_STRING(diActId));
	Q_sSearchString := CONCAT(Q_sSearchString,'.TMP.1.gSTATUS');
	Q_sSearchString := CONCAT(Q_sSearchString,F_ToCHR(I_sEndZeichen));
	Q_sSearchString := CONCAT(Q_sSearchString,F_ToCHR(I_sStartZeichen));
	Q_sSearchString := CONCAT(Q_sSearchString,DINT_TO_STRING(diActId));
	Q_sSearchString := CONCAT(Q_sSearchString,'.OUT.210.gSTATUS');
	Q_sSearchString := CONCAT(Q_sSearchString,F_ToCHR(I_sEndZeichen));
	
	iStep := iStep +1;
	
(*=========================================================================*)
3:	Q_sState := 'Suchen Aktiv';
	tonWait.IN := TRUE;
	Q_bSend := TRUE;
	
	fbPingEvent(
		I_sID:= 				DINT_TO_STRING(diActId), 
		I_sReceivedString:= 	I_StringReceived, 
		I_bReset:= ,
		Q_bOut=> );
		
	IF fbPingEvent.Q_bOut
	THEN
		Q_bSend := FALSE;
		iStep := iStep +1;
	ELSIF
		tonWait.Q
	THEN
		Q_bSend := FALSE;
		IF diActId <I_diMaxID
		THEN
			diActId := diActId +1;
			iStep := 1; (* nächsten Teilnehmer Suchen *)
		ELSE
			iStep := 6;	(* Scan Fertig *)
		END_IF
	END_IF
	
(*=========================================================================*)
4:	(* Teilnehmer gefunden ! *)
	Q_sState := 'Teilnehmer gefunden!';
	tonWait.IN := FALSE;	
	Q_bSend := FALSE;
	Q_sIDList[iArrIdx] := diActId;
	
	IF diActId <I_diMaxID
	THEN
		IF iArrIdx < cMaxScanIDs 
		THEN
			diActId := diActId +1;
			iArrIdx := iArrIdx +1;
			iStep := 1; (* nächsten Teilnehmer Suchen *)
		ELSE
			iStep := 5;
		END_IF
	ELSE
		iStep := 6;	(* Scan Fertig *)
	END_IF
	
(*=========================================================================*)
5:	(* Liste Voll! *)
	Q_sState := 'Liste Voll!';
	
(*=========================================================================*)
6:	(* Scan Fertig *)
	Q_iCnt := 0;
	
	FOR i := 1 TO cMaxScanIDs BY 1 DO 
		IF Q_sIDList[i] <> 0
		THEN
			Q_iCnt := Q_iCnt+1;
		END_IF
	END_FOR
	
	Q_sState := CONCAT('Scan Fertig! Es wurden: ',INT_TO_STRING(Q_iCnt));
	Q_sState := CONCAT(Q_sState,' Teilnehmer gefunden!');
		
	IF rTrigSearch.Q
	THEN
		iStep := 0;
	END_IF
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="fb_RS485IdScan">
      <LineId Id="19" Count="1" />
      <LineId Id="75" Count="0" />
      <LineId Id="83" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="240" Count="0" />
      <LineId Id="239" Count="0" />
      <LineId Id="241" Count="0" />
      <LineId Id="243" Count="0" />
      <LineId Id="242" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="213" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="77" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="172" Count="0" />
      <LineId Id="235" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="167" Count="3" />
      <LineId Id="28" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="174" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="64" Count="1" />
      <LineId Id="36" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="55" Count="2" />
      <LineId Id="63" Count="0" />
      <LineId Id="69" Count="1" />
      <LineId Id="72" Count="0" />
      <LineId Id="71" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="138" Count="1" />
      <LineId Id="89" Count="3" />
      <LineId Id="73" Count="0" />
      <LineId Id="93" Count="1" />
      <LineId Id="97" Count="0" />
      <LineId Id="120" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="101" Count="2" />
      <LineId Id="136" Count="0" />
      <LineId Id="106" Count="2" />
      <LineId Id="112" Count="0" />
      <LineId Id="109" Count="1" />
      <LineId Id="105" Count="0" />
      <LineId Id="96" Count="0" />
      <LineId Id="99" Count="0" />
      <LineId Id="113" Count="0" />
      <LineId Id="100" Count="0" />
      <LineId Id="215" Count="0" />
      <LineId Id="124" Count="0" />
      <LineId Id="121" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="173" Count="0" />
      <LineId Id="123" Count="0" />
      <LineId Id="129" Count="0" />
      <LineId Id="181" Count="1" />
      <LineId Id="193" Count="0" />
      <LineId Id="195" Count="1" />
      <LineId Id="186" Count="1" />
      <LineId Id="180" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="135" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="188" Count="0" />
      <LineId Id="190" Count="0" />
      <LineId Id="189" Count="0" />
      <LineId Id="198" Count="0" />
      <LineId Id="216" Count="0" />
      <LineId Id="197" Count="0" />
      <LineId Id="191" Count="0" />
      <LineId Id="223" Count="6" />
      <LineId Id="219" Count="0" />
      <LineId Id="230" Count="0" />
      <LineId Id="220" Count="2" />
      <LineId Id="231" Count="1" />
      <LineId Id="234" Count="0" />
      <LineId Id="233" Count="0" />
      <LineId Id="23" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>