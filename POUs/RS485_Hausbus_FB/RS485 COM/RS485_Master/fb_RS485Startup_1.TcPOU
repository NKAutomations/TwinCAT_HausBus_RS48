<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="fb_RS485Startup_1" Id="{a9d3934b-9506-44f6-b7ec-90fb21d1a0c0}" SpecialFunc="None">
    <Declaration><![CDATA[(*======================================================================================================================================*)
(*fb_RS485Startup:	Einfacher Baustein um die Hardware für die RS485 Haus-Bus vorzubereiten*)
(*
Date          	| Version    	| created under        	| Author          	| Remark *)
(*====================================================================================================================================================*)
(*13/05/2025   	| 1.00      		| TC3		  		| N.Kersting       | first release *)
(*====================================================================================================================================================*)
FUNCTION_BLOCK fb_RS485Startup_1
VAR_INPUT
	I_bExecute						:BOOL;
	I_bBusOK						:BOOL;
	I_tStartupVerz					:TIME := T#2S;	(* Startup verzögert ausführen *)
END_VAR
VAR_IN_OUT
	IQ_InData	 					:KL6inData22B;
    IQ_OutData						:KL6outData22B;
END_VAR
VAR_OUTPUT
	Q_sState						:STRING;
	Q_bStartupDone					:BOOL;
	Q_bError						:BOOL;
	Q_stStates						:ST_RS485Startup_State;
END_VAR
VAR
	udBaudrate						:UDINT := 57600;
	byDataBits						:BYTE := 8;
	byStopBits						:BYTE := 1;
	
	tonStartupVerz					:ton;
	fbSerialComReset				:ComReset;
	fComBufferReset					:ClearComBuffer;
	fbSerialConfig 					:KL6Configuration;  // Terminal-Konfiguration (Baud, Parität etc.)
	iStepStartup					:INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[tonStartupVerz(
	PT :=	I_tStartupVerz);
	
CASE iStepStartup OF 
(*=========================================================================*)
0:	IF NOT Q_bError
		AND I_bBusOK
	THEN
		Q_sState := 'INIT';
	ELSIF
		NOT I_bBusOK
	THEN
		Q_bError := TRUE;
		Q_sState := 'Busverbindung nicht Ok';
	END_IF
	
	tonStartupVerz.IN := FALSE;
	
	fbSerialComReset(
		Execute:= 		FALSE);
		
	fbSerialConfig(
		Execute:= 		FALSE);
		
	IF I_bExecute
		AND I_bBusOK
	THEN
		Q_bStartupDone := FALSE;
		iStepStartup := iStepStartup+1;
	END_IF

(*=========================================================================*)
1:	Q_sState := 'Warten auf Verzögerung';
	tonStartupVerz.IN := TRUE; 
	
	IF tonStartupVerz.Q
	THEN
		iStepStartup := iStepStartup+1;
	END_IF
	
(*=========================================================================*)
2:	Q_sState := 'Serielle Klemme Konfigurieren mit Haus-Bus Vorgaben';	
	
	fbSerialConfig(
		Execute:= 		TRUE, 
		Mode:= 			ComSerialLineMode_t.SERIALLINEMODE_KL6_22B_STANDARD,		 
		Baudrate:= 		udBaudrate, 
		NoDatabits:= 	byDataBits, 
		Parity:= 		ComParity_t.PARITY_EVEN,	 
		Stopbits:= 		byStopBits, 
		Handshake:= 	RS485_HALFDUPLEX, 
		ContinousMode:= , 
		pComIn := 		ADR(IQ_InData), 
		pComOut := 		ADR(IQ_OutData), 
		SizeComIn := 	SIZEOF(IQ_InData),
		Done=> , 
		Busy=> , 
		Error=> , 
		ErrorId=> );
	
	IF fbSerialConfig.Done
	THEN
		fbSerialConfig(
			Execute:= 		FALSE); 
			
		iStepStartup := 3;
	ELSIF
		fbSerialConfig.Error
	THEN
		Q_sState := 'Error fbSerialConfig ID: ';
		Q_sState := CONCAT(Q_sState,INT_TO_STRING(fbSerialConfig.ErrorID));
		iStepStartup := -2;	//Error
	END_IF

(*=========================================================================*)
3:	Q_sState := 'Serielle Klemme Reset Auführen';	
		
	fbSerialComReset(
		Execute:= 		TRUE, 
		pComIn := 		ADR(IQ_InData), 
		pComOut := 		ADR(IQ_OutData), 
		SizeComIn := 	SIZEOF(IQ_InData),
		Done=> , 
		Busy=> , 
		Error=> , 
		ErrorID=> );	
		
	IF fbSerialComReset.Done
	THEN
		iStepStartup := 999;
	ELSIF
		fbSerialComReset.Error
	THEN
		Q_sState := 'Error fbSerialComReset ID: ';
		Q_sState := CONCAT(Q_sState,INT_TO_STRING(fbSerialComReset.ErrorID));
		iStepStartup := -3;	//Error
	END_IF
	
(*=========================================================================*)
999:Q_sState := 'Startup Ausgeführt!';	
	Q_bStartupDone := TRUE;
	Q_bError := FALSE;
	
	IF NOT I_bBusOK
	THEN
		Q_bStartupDone := FALSE;
		iStepStartup := 0;
	END_IF

ELSE
	(* ERROR *)
	Q_bError := TRUE;
	iStepStartup := 0;
END_CASE

(* StatusStruktur für Visu *)
Q_stStates.bBusOk				:=	I_bBusOK;
Q_stStates.bStartupDone			:=	Q_bStartupDone;
Q_stStates.bStartupError		:=	Q_bError;
Q_stStates.sStartupState		:=	Q_sState;]]></ST>
    </Implementation>
    <LineIds Name="fb_RS485Startup_1">
      <LineId Id="36" Count="2" />
      <LineId Id="9" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="153" Count="1" />
      <LineId Id="156" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="150" Count="1" />
      <LineId Id="25" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="50" Count="3" />
      <LineId Id="39" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="28" Count="1" />
      <LineId Id="31" Count="0" />
      <LineId Id="112" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="40" Count="3" />
      <LineId Id="45" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="48" Count="0" />
      <LineId Id="46" Count="1" />
      <LineId Id="57" Count="25" />
      <LineId Id="146" Count="1" />
      <LineId Id="83" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="89" Count="2" />
      <LineId Id="84" Count="0" />
      <LineId Id="93" Count="12" />
      <LineId Id="140" Count="2" />
      <LineId Id="144" Count="1" />
      <LineId Id="143" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="106" Count="0" />
      <LineId Id="108" Count="1" />
      <LineId Id="107" Count="0" />
      <LineId Id="111" Count="0" />
      <LineId Id="138" Count="0" />
      <LineId Id="127" Count="1" />
      <LineId Id="131" Count="0" />
      <LineId Id="130" Count="0" />
      <LineId Id="129" Count="0" />
      <LineId Id="133" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="134" Count="1" />
      <LineId Id="139" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="198" Count="0" />
      <LineId Id="194" Count="0" />
      <LineId Id="193" Count="0" />
      <LineId Id="195" Count="2" />
    </LineIds>
  </POU>
</TcPlcObject>